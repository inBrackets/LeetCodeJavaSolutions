plugins {
    id 'java'
}

wrapper {
    gradleVersion = '8.7'
}

repositories {
    mavenCentral()
}

group = 'org.allure'
version = '1.0-SNAPSHOT'

def allureVersion = '2.29.0'
def aspectJVersion = '1.9.22'
def junitVersion = '5.11.3'
def selenideVersion = '7.5.1'
def slf4jVersion = '2.0.16'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
}

configurations {
    agent {
        canBeResolved = true
        canBeConsumed = true
    }
}

dependencies {
    agent "org.aspectj:aspectjweaver:$aspectJVersion"

    testImplementation "org.slf4j:slf4j-simple:$slf4jVersion"

    testImplementation platform("org.junit:junit-bom:$junitVersion")
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"

    testImplementation "com.codeborne:selenide:$selenideVersion"

    testImplementation platform("io.qameta.allure:allure-bom:$allureVersion")
    testImplementation 'io.qameta.allure:allure-junit5'
    testImplementation 'io.qameta.allure:allure-selenide'
}

test {
    useJUnitPlatform()
    jvmArgs = [ "-javaagent:${configurations.agent.singleFile}" ]
    testLogging {
		events "failed"
		exceptionFormat "full"
	}
}
